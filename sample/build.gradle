buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
      classpath 'com.android.tools.build:gradle:0.12.+'
      classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
      classpath 'com.jakewharton.hugo:hugo-plugin:1.1.+'
  }
}

// Manifest version information!
def versionMajor = 0
def versionMinor = 5
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'android-sdk-manager'
apply plugin: 'android'
apply plugin: 'hugo'

repositories {
  mavenCentral()
}

dependencies {
  compile project(':library')
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion 20
  buildToolsVersion "20"

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled
  }

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 20

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  buildTypes {
    debug {
//      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}