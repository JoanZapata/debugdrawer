//import com.android.builder.core.BuilderConstants

import java.util.regex.Pattern;

buildscript {
    repositories {  mavenCentral() }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.+'
        classpath 'com.github.dcendents:android-maven-plugin:1.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'hugo'
apply plugin: 'android-maven' // allows for gradle install to work, to install to local repo

repositories { mavenCentral() }
dependencies {
    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.jakewharton:butterknife:5.0.1'
    compile 'com.jakewharton.timber:timber:2.2.2'
    compile 'com.jakewharton.madge:madge:1.1.1'
    compile 'com.jakewharton.scalpel:scalpel:1.1.1'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 20
    buildToolsVersion "20"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateDebugBuildConfig') {
        task.dependsOn 'incrementVersionName'
    }
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'increaseVersionCode'
        task.dependsOn 'incrementVersionName'
    }
}

apply from: 'https://raw.github.com/6thSigma/gradle-mvn-push/master/gradle-mvn-push.gradle'